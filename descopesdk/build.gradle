plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "maven-publish"
}

android {
    namespace "com.descope"
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1"
    implementation "androidx.lifecycle:lifecycle-common:2.6.1"
    implementation "androidx.lifecycle:lifecycle-process:2.6.1"
    implementation "androidx.security:security-crypto:1.0.0"
    implementation "androidx.browser:browser:1.5.0"

    // Test Dependencies
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.1"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.json:json:20230227"
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = "com.descope"
            artifactId = "descope-kotlin"
            version = "0.9.0"

            afterEvaluate {
                from components.release
            }
        }
    }

  repositories {
    maven {
      name = "OSSRH"
      url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = System.getenv("MAVEN_USERNAME")
        password = System.getenv("MAVEN_PASSWORD")
      }
    }
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/descope/descope-kotlin"
      credentials {
        username = System.getenv("GITHUB_ACTOR")
        password = System.getenv("GITHUB_TOKEN")
      }
    }
  }
}