plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "maven-publish"
    id "signing"
}

android {
    namespace "com.descope"
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.0"
    implementation "androidx.lifecycle:lifecycle-common:2.7.0"
    implementation "androidx.lifecycle:lifecycle-process:2.7.0"
    implementation "androidx.browser:browser:1.7.0"
    implementation "androidx.security:security-crypto:1.0.0"
    implementation "androidx.credentials:credentials:1.2.0"
    implementation "androidx.credentials:credentials-play-services-auth:1.2.1"
    implementation "com.google.android.libraries.identity.googleid:googleid:1.1.0"
    implementation "com.google.android.gms:play-services-fido:20.1.0"

    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.8.0"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.json:json:20231013"
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = "com.descope"
            artifactId = "descope-kotlin"
            version = System.getenv("DESCOPESDK_VERSION")
            pom {
                name = project.name
                description = project.name
                url = 'https://github.com/descope/descope-kotlin'
                inceptionYear = '2023'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'descope'
                        name = 'Descope Inc'
                    }
                }
                scm {
                    connection = 'scm:https://github.com/descope/descope-kotlin.git'
                    developerConnection = 'scm:git@github.com:descope/descope-kotlin.git'
                    url = 'https://github.com/descope/descope-kotlin'
                }
            }
            afterEvaluate {
                from components.release
            }
        }
    }
}

signing {
    def signingKey = System.getenv("PGP_KEY")
    def signingPassword = System.getenv("PGP_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.release 
}